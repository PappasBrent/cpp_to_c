start                       =   translation_unit;

translation_unit            =   [external_declaration_list];

external_declaration_list   =   external_declaration, [external_declaration_list];

external_declaration        =   declaration
                            |   function_definition;

declaration                 =   declaration_specs, [init_declarator_list], ';';

(*
Note: This grammar imposes greater retrictions on the number of
declaration specifiers than C99 or ASNI C standards.
This makes the grammar easier to reason about, but will
have to be changed eventually to be less restrictive.

This production only allows for at most one storage class specifier since
legal C declarations contain at most one storage class specifier.

This production only allows for at most one type qualifer (which could be a
combination of C's two type qualifiers; see the correpsonding production rule)
on a declarator, since even though a redundant application of the same
qualifier is technically legal C, it parses to the same thing as using
that qualifier only once.

Functions/variables/typedefs whose declaration specifiers lack a type_specifier
nonterminal default to the `int` type specifier.
*)
declaration_specs           =   storage_class_specifier
                            |   storage_class_specifier, type_qualifier
                            |   storage_class_specifier, type_qualifier, type_specifier
                            |   storage_class_specifier, type_specifier
                            |   storage_class_specifier, type_specifier, type_qualifier
                            |   type_qualifier
                            |   type_qualifier, storage_class_specifier
                            |   type_qualifier, storage_class_specifier, type_specifier
                            |   type_qualifier, type_specifier
                            |   type_qualifier, type_specifier, storage_class_specifier
                            |   type_specifier
                            |   type_specifier, storage_class_specifier
                            |   type_specifier, storage_class_specifier, type_qualifier
                            |   type_specifier, type_qualifier
                            |   type_specifier, type_qualifier, storage_class_specifier;

storage_class_specifier     =   'typedef' | 'extern' | 'static' | 'auto' | 'register';

(* Includes all combinations of C's type qualifiers. *)
type_qualifier              =   'const', ['volatile'] | 'volatile', ['const'];

type_specifier              =   ? any c primitive type or defined type ?
                            |   struct_or_union_specifier
                            |   enum_specifier;

struct_or_union_specifier   =   ('struct' | 'union'), [identifier], '{', struct_declaration_list, '}'
                            |   ('struct' | 'union'), identifier;

struct_declaration_list     =   struct_declaration, [struct_declaration_list];

struct_declaration          =   specifier_qualifier_list, struct_declarator_list, ';';

specifier_qualifier_list    =   (type_specifier | type_qualifier), [specifier_qualifier_list];

struct_declarator_list      =   struct_declarator, ',', [struct_declarator_list];

struct_declarator           =   declarator, [':', constant_expression]
                            |   ':', constant_expression;

enum_specifier              =   'enum', identifier, ['{', enumerator_list, '}']
                            |   'enum', '{', enumerator_list, '}';

enumerator_list             =   enumerator, ',', [enumerator_list];

enumerator                  =   identifier, ['=', constant_expression];

init_declarator_list        =   init_declarator, [',', init_declarator_list];

init_declarator             =   declarator, ['=', initializer];

declarator                  =   [pointer], direct_declarator;

pointer                     =   '*' [[type_qualifier], pointer];

direct_declarator           =   identifier
                            |   '(', declarator, ')'
                            |   direct_declarator, '[', [constant_expression], ']'
                            |   direct_declarator, '(', [parameter_type_list | identifier_list], ')';

identifier                  =   ? any valid C identifier ?;

(*
This is missing the produciton rule for ellipsis because we do not support
variadic functions.
*)
parameter_type_list         =   parameter_list;

parameter_list              =   parameter_declaration, [',', parameter_list];

parameter_declaration       =   declaration_specs, [declarator | abstract_declarator];

abstract_declarator         =   pointer
                            |   [pointer], direct_abstract_declarator;

direct_abstract_declarator  =   '(', abstract_declarator, ')'
                            |   [direct_abstract_declarator], '[', [constant_expression], ']'
                            |   [direct_abstract_declarator], '(', [parameter_type_list], ')';

identifier_list             =   identifier, [',', identifier_list];

initializer                 =   assignment_expression;

assignment_expression       =   conditional_expression
                            |   {pointer}, identifier, assignment_operator, assignment_expression;

assignment_operator         =   ? any c operator concatenated with '=' ?;

conditional_expression      =   logical_or_expression, ['?', expression, ':', conditional_expression];

logical_or_expression       =   [logical_or_expression, '||'], logical_and_expression;

logical_and_expression      =   [logical_and_expression, '&&'], inclusive_or_expression;

inclusive_or_expression     =   [inclusive_or_expression, '|'], exclusive_or_expression;

exclusive_or_expression     =   [exclusive_or_expression, '^'], and_expression;

and_expression              =   [and_expression, '&'], equality_expression;

equality_expression         =   [equality_expression, ('==' | '!=')], relational_expression;

relational_expression       =   [relational_expression, ('<' | '>' | '<=' | '>=')], shift_expression;

shift_expression            =   [shift_expression, ('<<' | '>>')], additive_expression;

additive_expression         =   [additive_expression, ('+' | '-')], multiplicative_expression;

multiplicative_expression   =   [multiplicative_expression, ('*' | '/' | '%')], cast_expression;

cast_expression             =   unary_expression
                            |   '(', type_specifier, ')', cast_expression;

unary_expression            =   postfix_expression
                            |   ('++' | '--' | 'sizeof'), unary_expression
                            |   ('&' | '*' | '+' | '~' | '-' | '!'), cast_expression
                            |   'sizeof', '(', type_specifier, ')';

postfix_expression          =   primary_expression
                            |   postfix_expression, ('++' | '--')
                            |   identifier, '(', [argument_expression_list], ')';

argument_expression_list    =   assignment_expression, [',', argument_expression_list];

primary_expression          =   identifier
                            |   constant
                            |   string_literal
                            |   '(', expression, ')';

constant                    =   ? any valid C numeric literal value ?;

string_literal              =   ? any valid C string literal ?;

function_definition         =   declaration_specs, declarator, parameter_list, compound_statement;

parameter_list              =   ? a valid list of function parameters ?;

(*
Note: This production rule for compound statements specifies that a
compound statement may not contain a mix of declarations and statements.
This is in strict adherence to ANSI C / C89 standards.
*)
compound_statement          =   '{', declaration_list, '}'
                            |   '{', statement_list, '}'
                            |   '{', declaration_list, statement_list, '}';

statement_list              =   statement, [statement_list];

statement                   =   labeled_statement
                            |   compound_statement
                            |   expression_statement
                            |   selection_statement
                            |   iteration_statement
                            |   jump_statement;

labeled_statement           =   (identifier | 'case', constant_expression | 'default'), ':', statement;

constant_expression         =   conditional_expression;

expression_statement        =   [expression], ';';

expression                  =   assignment_expression, [',', expression];

selection_statement         =   'if', '(', expression, ')', statement, ['else', statement]
                            |   'switch', '(', expression, ')', statement;

iteration_statement         =   'while', '(', expression, ')', statement
                            |   'do', statement, 'while', '(', expression, ')', ';'
                            |   'for', '(', expression_statement, expression_statement, [expression], ')', statement;

jump_statement              =   'goto', identifier, ';'
                            |   'continue', ';'
                            |   'break', ';'
                            |   'return', [expression], ';';
